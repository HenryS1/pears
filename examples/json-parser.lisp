(defpackage json-benchmark
  (:use :cl :pears))

(in-package :json-benchmark)

(defun json ()
  (sequential (_ (ignore-whitespace))
              (v (orp (json-string)
                      (json-number)
                      (json-object)
                      (json-array)
                      (json-boolean)
                      (json-null)))
              v))

(defun json-boolean ()
  (orp (sequential (_ (seq "true")) t) 
       (sequential (_ (seq "false")) nil)))

(defun json-null ()
  (sequential (_ (seq "null")) nil))

(defun hexp (c)
  (let ((code (char-code c)))
    (or (<= 48 code 57)
        (<= 65 code 70)
        (<= 97 code 102))))

(defun unicode-char ()
  (sequential (_ (char1 #\\))
              (_ (char1 #\u))
              (cs (manyn #'hexp 4))
              (code-char (parse-integer cs :radix 16))))

(defun escaped-character ()
  (orp 
   (unicode-char)
   (sequential (_ (char1 #\\))
               (c (orp (char1 #\")
                       (char1 #\\)
                       (char1 #\/)
                       (sequential (_ (char1 #\b)) #\backspace)
                       (sequential (_ (char1 #\f)) #\formfeed)
                       (sequential (_ (char1 #\n)) #\linefeed)
                       (sequential (_ (char1 #\r)) #\return)
                       (sequential (_ (char1 #\t)) #\tab)))
               c)))

(defun json-string ()
  (sequential (_ (char1 #\"))
              (cs (repeated (orp (many1 (lambda (c) 
                                          (and (not (char= c #\\))
                                               (not (char= c #\")))))
                                 (escaped-character))))              
              (_ (char1 #\"))
              (format nil "狺泱┅ㄤ彐躅牦镱脲鲠祯ī箦聃孱糸犰ㄟㄩ珙矧瀛麒轸弩疳沐┅脲牦镱篝蜷铉┅ㄟㄩ珙矧瀛麒轸弩疳沐┅ㄟㄣ栳虮＼憨鲠祯牦镱┅ㄟㄩ珙矧瀛麒轸弩疳沐┅ㄣ镱脲鲠祯濠┅ㄤ彐躅犰轶舡麸栳箬翎忪ㄡ扉篝戾è翎忪磲脲栳箬翎忪呼弩у聃犰后辁戾铉翳犰轶舂┅磲疸灬礅溽ㄥ箦翩ㄧ弭栳箬ㄣ狎濠翎忪濠ㄣ潋濠┅犰轶舂翎忪濠ㄤ彐躅牦镱镡赍泗ī箦聃孱糸犰ㄟㄩ珙矧瀛麒轸弩疳沐┅ㄟㄣ栳虮＼┅ㄟㄩ珙矧瀛麒轸弩疳沐┅脲蟓犷洵鲠祯弩箦瓠怡牦镱脲鲠祯濠ㄣ栳虮＼┅ㄟㄩ珙矧瀛麒轸弩疳沐┅ㄟㄣ栳虮＼┅ㄡ扉篝麸栳箬翎忪脲蟓犷洵鲠祯弩┅ㄤ彐躅溟玳趔麸轭ㄤ殓轸螬祜镳骘徙蝻篌溟玳趔骘ㄤ殓轸汨狎洎翳孱ǐí卑ㄤ殓轸汨狎洎骈钺祆蝈趱蝾瞟┅ㄤ彐躅牦镱狎蜥ī箦聃孱糸犰ㄟㄣ栳虮＼郓ㄩ箦瓠怡箦聃孱糸犰牦镱┅ㄟㄩ珙矧瀛麒轸弩疳沐┅雯ㄣ栳虮＼┅ㄟㄣ栳虮＼荸轶┅ㄤ彐躅骝徙糸镱犰疳螋ī箦聃孱糸犰ㄟㄣ栳虮＼┅ㄤ磲铢＇溟玳舡汨狎皓ǒㄤ殓轸蟓麸轭潴ㄥ痿卑戾铉翳潴┅┅ㄤ彐躅屮痫铄铘疳螋ī箦聃孱糸犰ㄟ矧ㄣ栳虮＼濠ㄣ栳虮＼农┅镳矧箦聃孱糸犰ㄟㄣ栳虮＼┅＇箦聃孱糸犰ㄟㄣ栳虮＼┅＇┅ㄥ铒瞽铄玑糸鲥轭舄灬礅溽瞟ㄦ躅汜祆镳ㄥ痿卑屮┅┅ㄤ彐躅轭翦珧犰疳螋ī箦聃孱糸犰ㄩ矧箦聃孱糸犰ㄟㄣ栳虮＼癌癌痫箝糸鲥轭舄┅椹ㄤ彐躅牦镱铛礅弪ī箦聃孱糸犰铄玑翦镳糸镱犰ㄣ栳虮＼┅ㄩ铘ㄩ铘彗蜥飙疳螋┅ㄦ镳糸镱犰ㄦ蜥泗轱钺飙疳螋┅ㄥ镳糸镱犰ㄥ痫铄铘疳螋┅戾舄èㄩǐㄣ狎姗轭舂轭舂鏖翳屮ㄩㄦ躅汜祆ㄣ狎濠瞟瞟┅ㄩ铄玑翦ō鏖翳屮皓鏖翳屮皓┅ㄤ彐躅蝓瞽忮钽桧狎ī戾è溟蝈泗矧ㄡ箐婧簌篝屙箫躜沐溟蝈泗矧吼遽蝮┅糸礤鏖翳镳孱骈戾ㄦ殪ㄦ矧磲铋岑翦篝蟑灬蜱暹骈戾牦镱溟蝈泗矧┅疳蝮瀛篝蝈犴牦镱骈戾┅┅